/*
 * build.gradle
 *
 * Copyright (C) io.github.longfish801 All Rights Reserved.
 */

plugins {
	id 'com.github.monosoul.markdown.page.generator' version '2.1.0'
	id 'jacoco'
}

tasks.withType(AbstractCompile).each { it.options.encoding = 'UTF-8' }
tasks.withType(GroovyCompile).each { it.groovyOptions.encoding = 'UTF-8' }
tasks.withType(Javadoc).each { it.options.encoding = 'UTF-8' }

apply plugin: 'groovy'
apply plugin: 'maven-publish'

defaultTasks 'fix'
group = 'io.github.longfish801'
version = '0.3.02'

repositories {
	mavenCentral()
	maven { url 'https://longfish801.github.io/maven/' }
}

dependencies {
	implementation group: 'io.github.longfish801', name: 'gonfig', version: '0.2.00';
	implementation group: 'org.apache.commons', name: 'commons-text', version: '1.6'
	implementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.10'
	implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
	testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
	testImplementation group: 'org.spockframework', name: 'spock-core', version: '1.2-groovy-2.5'
}

test {
	systemProperty 'file.encoding', 'UTF-8'
	minHeapSize = '128m'
	maxHeapSize = '1024m'
}

jacocoTestReport {
	reports { html.destination file("${buildDir}/jacocoHtml") }
}
jacocoTestReport.dependsOn 'test'

groovydoc {
	overviewText = resources.text.fromFile('src/main/resources/overview.html')
	[
		'http://docs.oracle.com/javase/8/docs/api/' : 'java.,javax.,org.xml.',
		'https://docs.groovy-lang.org/latest/html/api/' : 'groovy.,org.codehaus.groovy.'
	].each { link(it.key, it.value); }
}

generateHtml {
	inputDirectory = file('src/markdown')
	headerHtmlFile = file('src/markdown/template/header.html')
	footerHtmlFile = file('src/markdown/template/footer.html')
	pegdownExtensions = 'TOC,EXTANCHORLINKS,HARDWRAPS,FENCED_CODE_BLOCKS,DEFINITIONS,TABLES'
}

publishing {
	publications {
		mavenJava(MavenPublication) { from components.java }
	}
	repositories {
		maven { url '../maven' }
    }
}

// サンプルを実行します
task execSample(type: JavaExec) {
	main = 'Sample'
	classpath = sourceSets.test.runtimeClasspath
}
task execSampleDsl(type: JavaExec) {
	main = 'SampleDsl'
	classpath = sourceSets.test.runtimeClasspath
}
task execSamples(dependsOn: [execSample, execSampleDsl]) {
}

// フィックスします
task fix(dependsOn: [clean, jacocoTestReport, execSamples, groovydoc, generateHtml]){
}

// マスターアップします
task deleteDocs(type: Delete){
	delete 'docs'
}
clean.dependsOn deleteDocs
task masterup(dependsOn: [groovydoc, generateHtml, deleteDocs]){
	doLast {
		copy {
			from 'build/html'
			into 'docs'
		}
		copy {
			from 'build/docs/groovydoc'
			into 'docs/groovydoc'
		}
	}
}

// リリースします
task release(dependsOn: [publish]){
}

